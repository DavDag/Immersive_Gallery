/**
 * This closure is a block that determines what is accessible to every other part of the
 * Build Script.
 *
 * https://docs.gradle.org/current/dsl/org.gradle.api.Script.html#org.gradle.api.Script:buildscript
 */
buildscript {

    /**
     * Project's extra properties.
     *
     * When reading the property, "ext" means "project.ext".
     * Usually when reading an extra property, the "ext" is omitted to favor a more concise access:
     *
     * "$project.ext.versions.fragment" => "$versions.fragment"
     *
     * https://docs.gradle.org/current/dsl/org.gradle.api.plugins.ExtraPropertiesExtension.html
     */
    ext.gradle_version = '7.2.1'
    ext.kotlin_version = '1.6.21'
    ext {
        minAndroidVersion = 25
        targetAndroidVersion = 31
        versions = [
                coreKtx: '1.7.0',
                appCompact: '1.4.1',
                material: '1.6.0',
                constraintLayout: '2.1.4',
                fragment: '1.4.1',
                navigation: '2.4.2',
                lifecycle: '2.4.1',
                hilt: '2.38.1',
                json_serialization: '1.3.2',
        ]
    }

    // Must be redefined here to download common dependencies
    repositories {
        google()
        mavenCentral()
    }

    // common dependencies
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$versions.navigation"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$versions.hilt"
    }
}

/**
 * Set containing each sub-project.
 *
 * This method is executed against this project and each of its sub-projects.
 *
 * https://docs.gradle.org/current/dsl/org.gradle.api.Project.html
 */
allprojects {

    /**
     * A RepositoryHandler manages a set of repositories, allowing repositories to be defined
     * and queried.
     *
     * https://docs.gradle.org/current/dsl/org.gradle.api.artifacts.dsl.RepositoryHandler.html
     */
    repositories {
        // Adds a repository which looks in Google's Maven repository for dependencies
        google()

        // Adds a repository which looks in the Maven central repository for dependencies
        mavenCentral()
    }
}

/**
 * Additional task to clean /build directory (ex. when performing a change to a gradle property).
 *
 * https://docs.gradle.org/current/dsl/org.gradle.api.Task.html
 */
task clean(type: Delete) {
    delete rootProject.buildDir
}